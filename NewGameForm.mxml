<?xml version="1.0" encoding="utf-8"?>
<mx:TitleWindow xmlns:mx="http://www.adobe.com/2006/mxml" horizontalScrollPolicy="off" verticalScrollPolicy="off"
					paddingLeft="10" paddingBottom="8" paddingRight="5" paddingTop="5">
	    <mx:Script>
        <![CDATA[
		  import flash.events.Event;
		  import flash.net.FileReference;
          import mx.controls.*;
          import mx.managers.PopUpManager;
          import mx.collections.ArrayCollection;
		  import InfoFetcher;
		  
		  public var kifuRef:FileReference = new FileReference();
		  public var prev:String;
		  public var moves:Array;
		  
		  [Bindable]
		  public var handicaps:ArrayCollection = new ArrayCollection(
			[{label:"Even Game", data:"nr"},
			{label:"Lance Down", data:"hclance"},
			{label:"Bishop Down", data:"hcbishop" },
			{label:"Rook Down", data:"hcrook" },
			{label:"Rook-Lance Down", data:"hcrooklance" },
			{label:"Rook-Silver Down", data:"hcrooksilver" },
			{label:"2-piece Down", data:"hc2p" },
			{label:"4-piece Down", data:"hc4p" },
			{label:"6-piece Down", data:"hc6p" },
			{label:"8-piece Down", data:"hc8p" },
			{label:"Dragonfly", data:"hctombo" },
			{label:"10-piece Down", data:"hc10p"},
			{label:"Three Pawns", data:"hcfu3" },
			{label:"Naked King", data:"hcnaked" },
			{label:"Open-air Shogi", data:"vaoa" },
			{label:"Mini-shogi", data:"vamini" },
			{label:"GoroGoro-shogi", data:"va5656" },
			{label:"3x3 Shogi", data:"va33" } ]
		  );
		  
		  [Bindable]
		  public var tournaments:ArrayCollection = new ArrayCollection(
		  [{label:InfoFetcher.tournament_name_en[0], code:InfoFetcher.tournament_codes[0], type:"r", total:900, byoyomi:60},
			{label:InfoFetcher.tournament_name_en[1], code:InfoFetcher.tournament_codes[1], type:"r", total:900, byoyomi:60},
			{label:InfoFetcher.tournament_name_en[2], code:InfoFetcher.tournament_codes[2], type:"r", total:2700, byoyomi:60},
			{label:InfoFetcher.tournament_name_en[3], code:InfoFetcher.tournament_codes[3], type:"r", total:2700, byoyomi:60 },
			{label:InfoFetcher.tournament_name_en[4], code:InfoFetcher.tournament_codes[4], type:"nr", total:0, byoyomi:10 },
			{label:InfoFetcher.tournament_name_en[5], code:InfoFetcher.tournament_codes[5], type:"r", total:0, byoyomi:30 },
			{label:InfoFetcher.tournament_name_en[6], code:InfoFetcher.tournament_codes[6], type:"r", total:300, byoyomi:30 },
			{label:InfoFetcher.tournament_name_en[7], code:InfoFetcher.tournament_codes[7], type:"r", total:900, byoyomi:30 },
			{label:InfoFetcher.tournament_name_en[8], code:InfoFetcher.tournament_codes[8], type:"r", total:900, byoyomi:60 },
			{label:InfoFetcher.tournament_name_en[9], code:InfoFetcher.tournament_codes[9], type:"r", total:2700, byoyomi:90 }]
		  );

          private function submit():void {
            var total:int;
            var byoyomi:int;
			var handicap:String;
			var tournament:String;
            trace("Challenge Clicked");
            switch(timeSetting.selectedValue) {
			  case 0:
			    total = 2700;
				byoyomi = 60;
				handicap = "r";
				tournament = "";
				break;
              case 1:
                total = 900;
                byoyomi = 60;
				handicap = "r";
				tournament = "";
                break;
              case 2:
                total = 300;
                byoyomi = 30;
				handicap = "r";
				tournament = "";
                break;
              case 3:
                total = 0;
                byoyomi = 30;
				handicap = "r";
				tournament = "";
                break;
              case 4:
                total = nonrated_total.selectedItem.data;
                byoyomi = nonrated_byoyomi.selectedItem.data;
				handicap = nonrated_handicap.selectedItem.data;
//				if (parentApplication.serverName != "VENUS" && nonrated_handicap.selectedItem.data.match(/^va/)) return;
				tournament = "";
				break;
			  case 5:
				total = tournament_name.selectedItem.total;
				byoyomi = tournament_name.selectedItem.byoyomi;
				handicap = tournament_name.selectedItem.type;
				tournament = "--" + tournament_name.selectedItem.code;
				break;
			  case 6:
				dispatchEvent(new Event("study_room"));
				PopUpManager.removePopUp(this);
				return;
              default:
                break;
            }
            if(total >=0 && byoyomi){
              var gameRuleEvent:GameRuleEvent = new GameRuleEvent(GameRuleEvent.RULE_SELECTED,total,byoyomi,handicap,tournament);
              dispatchEvent(gameRuleEvent);
			  trace("created");
            }
            PopUpManager.removePopUp(this);
          }

          public function toggleTimeSelection():void{
            nonrated_total.enabled = timeSetting.selectedValue == 4;
            nonrated_byoyomi.enabled = timeSetting.selectedValue == 4;
            nonrated_handicap.enabled = timeSetting.selectedValue == 4;
			tournament_name.enabled = timeSetting.selectedValue == 5;
			study_handicap.enabled = timeSetting.selectedValue == 6;
			blackName.enabled = timeSetting.selectedValue == 6;
			whiteName.enabled = timeSetting.selectedValue == 6;
			loadKifuButton.enabled = timeSetting.selectedValue == 6;
			kifuName.enabled = timeSetting.selectedValue == 6;
          }
		  
		  private function _loadKifu():void {
			  kifuRef.addEventListener(Event.SELECT, _handleKifuSelect);
			  kifuRef.addEventListener(Event.COMPLETE, _handleKifuComplete);
			  kifuRef.browse();
		  }
		  
		  private function _handleKifuSelect(e:Event):void {
			  moves = new Array();
			  kifuName.text = FileReference(e.target).name;
			  FileReference(e.target).load();
		  }
		  
		  private function _handleKifuComplete(e:Event):void {
			  var hirate:Boolean = false;
			  for each (var line:String in (String(FileReference(e.target).data)).split("\n")) {
				if (line.match(/^手合割：平手/)) {
					hirate = true;
					continue;
				}
				var match:Array = line.match( /^\s+(\d+)\s+(同|[１２３４５６７８９][一二三四五六七八九])\s*(成?)([歩と香桂銀金角馬飛龍竜玉王])(不?成?)(\(\d+\)|打)/);
				if (!match) continue;
				var csa:String = "";
				if (parseInt(match[1]) % 2 == (hirate ? 0 : 1)) {
					csa += "-";
				} else {
					csa += "+";
				}
				if (match[6] == "打") {
					csa += "00";
				} else {
					csa += match[6].substr(1, 2);
				}
				if (match[2] == "同") {
					csa += prev;
				} else {
					switch (match[2].charAt(0)) {
						case "１":
							csa += "1"; prev = "1"; break;
						case "２":
							csa += "2"; prev = "2"; break;
						case "３":
							csa += "3"; prev = "3"; break;
						case "４":
							csa += "4"; prev = "4"; break;
						case "５":
							csa += "5"; prev = "5"; break;
						case "６":
							csa += "6"; prev = "6"; break;
						case "７":
							csa += "7"; prev = "7"; break;
						case "８":
							csa += "8"; prev = "8"; break;
						case "９":
							csa += "9"; prev = "9"; break;
					}
					switch (match[2].charAt(1)) {
						case "一":
							csa += "1"; prev += "1"; break;
						case "二":
							csa += "2"; prev += "2"; break; 
						case "三":
							csa += "3"; prev += "3"; break;
						case "四":
							csa += "4"; prev += "4"; break;
						case "五":
							csa += "5"; prev += "5"; break;
						case "六":
							csa += "6"; prev += "6"; break;
						case "七":
							csa += "7"; prev += "7"; break;
						case "八":
							csa += "8"; prev += "8"; break;
						case "九":
							csa += "9"; prev += "9"; break;
					}
				}
				if (match[3] == "成" || match[5] == "成") {
					switch (match[4]) {
						case "歩":
							csa += "TO"; break;
						case "香":
							csa += "NY"; break;
						case "桂":
							csa += "NK"; break;
						case "銀":
							csa += "NG"; break;
						case "角":
							csa += "UM"; break;
						case "飛":
							csa += "RY"; break;
					}
				} else {
					switch (match[4]) {
						case "歩":
							csa += "FU"; break;
						case "と":
							csa += "TO"; break;
						case "香":
							csa += "KY"; break;
						case "桂":
							csa += "KE";  break;
						case "銀":
							csa += "GI"; break;
						case "金":
							csa += "KI"; break;
						case "角":
							csa += "KA"; break;
						case "馬":
							csa += "UM"; break;
						case "飛":
							csa += "HI"; break;
						case "龍":
							csa += "RY"; break;
						case "竜":
							csa += "RY"; break;
						case "玉":
							csa += "OU"; break;
						case "王":
							csa += "OU"; break;
					}
				}
				moves.push(csa);
			  }
		  }

        ]]>
    </mx:Script>
	<mx:RadioButtonGroup id="timeSetting" change="toggleTimeSelection();"/>
	<mx:RadioButton groupName="timeSetting" label="45{parentApplication.lan.min} - 60{parentApplication.lan.sec} , {parentApplication.lan.rated} (x2.0)" value="0" fontSize="11"/>
	<mx:RadioButton groupName="timeSetting" label="15{parentApplication.lan.min} - 60{parentApplication.lan.sec} , {parentApplication.lan.rated} (x1.3)" value="1" fontSize="11"/>
	<mx:RadioButton groupName="timeSetting" label=" 5{parentApplication.lan.min} - 30{parentApplication.lan.sec} , {parentApplication.lan.rated} (x1.0)" value="2" fontSize="11"/>
	<mx:RadioButton groupName="timeSetting" label=" 0{parentApplication.lan.min} - 30{parentApplication.lan.sec} , {parentApplication.lan.rated} (x0.8)" value="3" fontSize="11"/>
	<mx:RadioButton groupName="timeSetting" label="{parentApplication.lan.nonrated}" value="4" fontSize="11"/>
	<mx:HBox paddingLeft="50">
		<mx:ComboBox id="nonrated_total" enabled="false" selectedIndex="3">
		  <mx:ArrayCollection>
			 <mx:Object label=" 0{parentApplication.lan.min}" data="0"/>
			 <mx:Object label=" 5{parentApplication.lan.min}" data="300"/>
			 <mx:Object label="10{parentApplication.lan.min}" data="600"/>
			 <mx:Object label="15{parentApplication.lan.min}" data="900"/>
			 <mx:Object label="20{parentApplication.lan.min}" data="1200"/>
			 <mx:Object label="30{parentApplication.lan.min}" data="1800"/>
			 <mx:Object label="45{parentApplication.lan.min}" data="2700"/>
			 <mx:Object label="60{parentApplication.lan.min}" data="3600"/>
		  </mx:ArrayCollection>
		</mx:ComboBox>
		<mx:ComboBox id="nonrated_byoyomi" enabled="false" selectedIndex="4">
		  <mx:ArrayCollection>
			 <mx:Object label="10{parentApplication.lan.sec}" data="10"/>
			 <mx:Object label="20{parentApplication.lan.sec}" data="20"/>
			 <mx:Object label="30{parentApplication.lan.sec}" data="30"/>
			 <mx:Object label="60{parentApplication.lan.sec}" data="60"/>
		  </mx:ArrayCollection>
		</mx:ComboBox>
	</mx:HBox>
	<mx:HBox paddingLeft="50">
		<mx:ComboBox id="nonrated_handicap" selectedIndex="0" enabled="false">
		  <mx:ArrayCollection>
			 <mx:Object label="{parentApplication.lan.hc_even}" data="nr"/>
			 <mx:Object label="{parentApplication.lan.hc_lance}" data="hclance"/>
			 <mx:Object label="{parentApplication.lan.hc_bishop}" data="hcbishop"/>
			 <mx:Object label="{parentApplication.lan.hc_rook}" data="hcrook"/>
			 <mx:Object label="{parentApplication.lan.hc_rooklance}" data="hcrooklance"/>
			 <mx:Object label="{parentApplication.lan.hc_rooksilver}" data="hcrooksilver"/>
			 <mx:Object label="{parentApplication.lan.hc_2p}" data="hc2p"/>
			 <mx:Object label="{parentApplication.lan.hc_4p}" data="hc4p"/>
			 <mx:Object label="{parentApplication.lan.hc_6p}" data="hc6p"/>
			 <mx:Object label="{parentApplication.lan.hc_8p}" data="hc8p"/>
			 <mx:Object label="{parentApplication.lan.hc_tombo}" data="hctombo"/>
			 <mx:Object label="{parentApplication.lan.hc_10p}" data="hc10p"/>
			 <mx:Object label="{parentApplication.lan.hc_3pawns}" data="hcfu3"/>
			 <mx:Object label="{parentApplication.lan.hc_naked}" data="hcnaked"/>
			 <mx:Object label="{parentApplication.lan.hc_openair}" data="vaoa"/>
			 <mx:Object label="{parentApplication.lan.hc_mini}" data="vamini"/>
			 <mx:Object label="{parentApplication.lan.hc_56}" data="va5656"/>
			 <mx:Object label="{parentApplication.lan.hc_33}" data="va33"/>
		  </mx:ArrayCollection>
		</mx:ComboBox>
    </mx:HBox>
    <mx:RadioButton groupName="timeSetting" label="{parentApplication.lan.tournament}" value="5" fontSize="11"/>
	<mx:HBox paddingLeft="50">
		<mx:ComboBox id="tournament_name" selectedIndex="0" dataProvider="{tournaments}" enabled="false"/>
    </mx:HBox>
	<mx:RadioButton groupName="timeSetting" label="{parentApplication.lan.study_room}" value="6" fontSize="11"/>
	<mx:HBox paddingLeft="50">
		<mx:ComboBox id="study_handicap" selectedIndex="0" dataProvider="{nonrated_handicap.dataProvider}" enabled="false"/>
    </mx:HBox>
	<mx:HBox paddingLeft="50">
		<mx:TextInput id="blackName" text="Black" width="65" enabled="false"/>
		<mx:TextInput id="whiteName" text="White" width="65" enabled="false"/>
    </mx:HBox>
	<mx:HBox paddingLeft="50">
		<mx:Button id="loadKifuButton" label="{parentApplication.lan.load_kifu}" enabled="false" click="_loadKifu();" />
		<mx:TextInput id="kifuName" editable="false" width="100" enabled="false" />
	</mx:HBox>
    <mx:HBox paddingTop="15">
	    <mx:Button id="Submit" click="submit();" label="{parentApplication.lan.wait}"/> 
	    <mx:Button label="{parentApplication.lan.cancel}" click="PopUpManager.removePopUp(this);"/> 
    </mx:HBox>
</mx:TitleWindow>
