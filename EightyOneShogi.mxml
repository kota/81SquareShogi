<?xml version="1.0" encoding="utf-8"?>
<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml" layout="absolute" creationComplete="{initApp()}" xmlns:eightyOneSquare="*">

	<mx:Script>
	  <![CDATA[
		import flash.display.*;
    import mx.events.FlexEvent;
		import mx.controls.*;
		import CsaShogiClient;
    import Board;

    private var _client:CsaShogiClient;
    [Bindable]
    private var _user_list:Array;

		private function initApp():void
		{
		  trace("application initialized..");
		  _client = new CsaShogiClient();
      board.setCallback(_playerMove);
      chatMessage.addEventListener(FlexEvent.ENTER,_handleSendChat);
      _user_list = new Array();
      userListGrid.dataProvider = _user_list;
		}

    private function _connectAndLogin():void{
		  trace("connecting.");
      _client.addEventListener(CsaShogiClient.CONNECTED,_handleConnected);
      _client.addEventListener(CsaShogiClient.LOGIN,_handleLoggedIn);
      _client.addEventListener(CsaShogiClient.GAME_STARTED,_handleGameStarted);
      _client.addEventListener(CsaShogiClient.GAME_END,_handleGameEnd);
      _client.addEventListener(CsaShogiClient.CHAT,_handleChat);
      _client.addEventListener(CsaShogiClient.MOVE,_handleMove);
      _client.addEventListener(CsaShogiClient.WHO,_handleWho);
      _client.connect();
    } 
    
    private function _handleConnected(e:Event):void{
		  trace("connected. try login.");
      _client.login(loginname.text, "password");
    } 

    private function _handleLoggedIn(e:Event):void{
      mainViewStack.selectedIndex = 1;
      _client.who();
      _writeUserMessage("Logged in successfully.");
    }

    private function _handleChat(e:ServerMessageEvent):void{
			_writeUserMessage(e.message);
    }

    private function _handleMove(e:ServerMessageEvent):void{
      trace("move "+e.message);
      board.makeMove(e.message);
    }

    private function _handleGameStarted(e:Event):void{
      if(_client.myTurn == Kyokumen.SENTE){
        _writeUserMessage("You are Sente(Black).");
        _writeUserMessage("Your turn.");
      } else {
        _writeUserMessage("You are Gote(White).");
      }
      board.startGame(_client.myTurn,_client.playerNames);
    }

    private function _handleGameEnd(e:ServerMessageEvent):void{
      if(e.message.indexOf("LOSE") >= 0){
        _writeUserMessage("You lose.");
      } else {
        _writeUserMessage("You win.");
      }
      board.endGame();
    }

    private function _handleWho(e:ServerMessageEvent):void{
      var users:Array = e.message.split("\n");
      trace(users.length.toString());
      var user_list:Array = new Array();
      for each(var user_data:String in users){
        trace(user_data);
        if(user_data.match("##[WHO] +OK") != null){
          break;
        }
        var match:Array = user_data.match(/\#\#\[WHO\] (.*)\+(.+)/);
        if(match != null && match[2] != "OK"){
          var name:String = match[1];
          trace(name);
          var user:Object = new Object();
          user.name = name;
          user.rating = 1000;
          user_list.push(user);
        }
      }
      userListGrid.dataProvider = user_list;
    }

    private function _writeUserMessage(message:String):void{
      userMessage.text += message + "\n";
      userMessage.callLater(_scrollDown);
    }

    private function _scrollDown():void{
      userMessage.verticalScrollPosition = userMessage.maxVerticalScrollPosition;
    }
    
    private function _waitForGame():void{
      _writeUserMessage("Waiting for other player to join...");
      _client.waitForGame();
    }

    private function _resign():void{
      _client.resign();
    }

    private function _who():void{
      _client.who();
    }

    private function _playerMove(from:Point,to:Point,promote:Boolean=false):void{
      var mv:Movement = board.position.generateMovementFromCoordinates(from,to,promote);
      _client.move(mv.toCSA());
    }

    private function _handleSendChat(e:FlexEvent):void{
      _client.chat(chatMessage.text);
      chatMessage.text = "";
    }

	  ]]>
	</mx:Script>
  <mx:ViewStack id="mainViewStack" creationPolicy="all" width="100%" height="100%">
    <mx:VBox id="loginBox" horizontalAlign="center" verticalAlign="middle" width="100%" height="100%">
      <mx:Form>
        <mx:FormItem label="loginname">
            <mx:TextInput id="loginname" width="200"/>
	      </mx:FormItem>
	      <mx:FormItem label="password">
            <mx:TextInput id="password" width="200" displayAsPassword="true"/>
	      </mx:FormItem>
	      <mx:FormItem>
            <mx:Button id="loginButton" label="Login" y="400" click="_connectAndLogin();"/>
	      </mx:FormItem>
	    </mx:Form>
    <mx:Label id="errorMessage"/>
      <mx:Label text="Message"/>
    </mx:VBox>
    <mx:VBox>
      <mx:DataGrid id="userListGrid" >
       <mx:columns>
        <mx:DataGridColumn dataField="name"/>
        <mx:DataGridColumn dataField="rating"/>
       </mx:columns>
      </mx:DataGrid>
    </mx:VBox>
    <mx:VBox>
      <eightyOneSquare:Board id="board" width="800" height="474"/> 
      <mx:HBox>
      <mx:Button id="startButton" label="Start" y="400" click="_waitForGame();"/>
      <mx:Button id="resignButton" label="Resign" y="400" click="_resign();"/>
      <mx:Button id="whoButton" label="Who's online" y="400" click="_who();"/>
      </mx:HBox>
      <mx:TextArea id="userMessage" wordWrap="true" editable="false" width="500" height="200"/>
      <mx:TextInput id="chatMessage" width="500"/>
    </mx:VBox>
  </mx:ViewStack>

</mx:Application>
