<?xml version="1.0" encoding="utf-8"?>
<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml" layout="absolute" creationComplete="{initApp()}" xmlns:eightyOneSquare="*">

	<mx:Script>
	  <![CDATA[
		import flash.display.*;
    import mx.events.FlexEvent;
		import mx.controls.*;
		import CsaShogiClient;
    import Board;

    private var _client:CsaShogiClient;
    [Bindable]
    private var _user_list:Array;
		private var _login_name:String;

		private function initApp():void
		{
		  trace("application initialized..");
		  _client = new CsaShogiClient();
      board.setMoveCallback(_playerMove);
      board.setTimeoutCallback(_checkTimeout);
      chatMessage.addEventListener(FlexEvent.ENTER,_handleSendChat);
      _user_list = new Array();
      userListGrid.dataProvider = _user_list;
		}

    private function _connectAndLogin():void{
		  trace("connecting.");
      _client.addEventListener(CsaShogiClient.CONNECTED,_handleConnected);
      _client.addEventListener(CsaShogiClient.LOGIN,_handleLoggedIn);
      _client.addEventListener(CsaShogiClient.GAME_STARTED,_handleGameStarted);
      _client.addEventListener(CsaShogiClient.GAME_END,_handleGameEnd);
      _client.addEventListener(CsaShogiClient.CHAT,_handleChat);
      _client.addEventListener(CsaShogiClient.MOVE,_handleMove);
      _client.addEventListener(CsaShogiClient.WHO,_handleWho);
      _client.connect();
    } 
    
    private function _handleConnected(e:Event):void{
      _client.login(loginname.text, "password");
			_login_name = loginname.text;
    } 

    private function _handleLoggedIn(e:Event):void{
      mainViewStack.selectedIndex = 1;
      _client.who();
    }

    private function _handleChat(e:ServerMessageEvent):void{
			_writeUserMessage(e.message);
    }

    private function _handleMove(e:ServerMessageEvent):void{
      board.makeMove(e.message);
    }

    private function _handleGameStarted(e:Event):void{
			userMessage.text = '';
      if(_client.myTurn == Kyokumen.SENTE){
        _writeUserMessage("You are Sente(Black).");
      } else {
        _writeUserMessage("You are Gote(White).");
      }
      board.startGame(_client.myTurn,_client.playerNames,10,10);
			closeButton.enabled = false;
      mainViewStack.selectedIndex = 2;
    }

    private function _handleGameEnd(e:ServerMessageEvent):void{
      if(e.message.indexOf("TIME_UP") >= 0){
				board.timeout();
        _writeUserMessage("Time out.");
			}
      if(e.message.indexOf("LOSE") >= 0){
        _writeUserMessage("You Lose.");
      } else {
        _writeUserMessage("You Win.");
      }
			closeButton.enabled = true;
      board.endGame();
    }

    private function _handleWho(e:ServerMessageEvent):void{
      var users:Array = e.message.split("\n");
      var user_list:Array = new Array();
      for each(var user_data:String in users){
        trace(user_data);
        if(user_data.match("##[WHO] +OK") != null){
          break;
        }
        var match:Array = user_data.match(/\#\#\[WHO\] (.*)\+(.+) x1 (\S*)/);
        if(match != null && match[2] != "OK"){
          var name:String = match[1];
          var user:Object = new Object();
          user.name = name;
          user.rating = 1000;
					user.status = match[3];
          user_list.push(user);
        }
      }
      userListGrid.dataProvider = user_list;
    }

    private function _writeUserMessage(message:String):void{
      userMessage.text += message + "\n";
      userMessage.callLater(_scrollDown);
    }

    private function _scrollDown():void{
      userMessage.verticalScrollPosition = userMessage.maxVerticalScrollPosition;
    }
    
    private function _waitForGame():void{
      //_writeUserMessage("Waiting for other player to join...");
      _client.waitForGame();
			_refresh();
    }

    private function _challenge():void{
			var user:Object = userListGrid.selectedItem;
			if(user != null){
      	_client.challenge(user.name);
			}
    }

		private function _refresh():void{
			_client.who();	
		}

    private function _resign():void{
      _client.resign();
    }

    private function _who():void{
      _client.who();
    }

		private function _closeGame():void{
      mainViewStack.selectedIndex = 1;
			_refresh();
		}

		private function _userSelected():void{
			var user:Object = userListGrid.selectedItem;
			if(user != null && user.name != _login_name){
				challengeButton.enabled = true;
			} else {
				challengeButton.enabled = false;
			}
		}

    private function _playerMove(from:Point,to:Point,promote:Boolean=false):void{
      var mv:Movement = board.position.generateMovementFromCoordinates(from,to,promote);
      _client.move(mv.toCSA());
    }

		private function _checkTimeout():void{
			_client.checkTimeout();
		}

    private function _handleSendChat(e:FlexEvent):void{
      _client.chat(chatMessage.text);
      chatMessage.text = "";
    }

	  ]]>
	</mx:Script>
  <mx:ViewStack id="mainViewStack" creationPolicy="all" width="100%" height="100%">
    <mx:VBox id="loginBox" horizontalAlign="center" verticalAlign="middle" width="100%" height="100%">
      <mx:Form>
        <mx:FormItem label="loginname">
            <mx:TextInput id="loginname" width="200"/>
	      </mx:FormItem>
	      <mx:FormItem label="password">
            <mx:TextInput id="password" width="200" displayAsPassword="true"/>
	      </mx:FormItem>
	      <mx:FormItem>
            <mx:Button id="loginButton" label="Login" y="400" click="_connectAndLogin();"/>
	      </mx:FormItem>
	    </mx:Form>
    <mx:Label id="errorMessage"/>
      <mx:Label text="Message"/>
    </mx:VBox>
    <mx:VBox>
			<mx:HBox>
        <mx:Button id="startButton" label="New Game" click="_waitForGame();"/>
			  <mx:Button id="challengeButton" label="Challenge" click="_challenge();" enabled="false"/>
			  <mx:Button id="refreshButton" label="Refresh" click="_refresh();"/>
			</mx:HBox>
      <mx:DataGrid id="userListGrid" width="600" change="_userSelected();">
       <mx:columns>
        <mx:DataGridColumn dataField="name"/>
        <mx:DataGridColumn dataField="rating"/>
        <mx:DataGridColumn dataField="status"/>
       </mx:columns>
      </mx:DataGrid>
    </mx:VBox>
    <mx:VBox>
      <eightyOneSquare:Board id="board" width="800" height="474"/> 
      <mx:HBox>
      <mx:Button id="resignButton" label="Resign" y="400" click="_resign();"/>
      <mx:Button id="closeButton" label="Close" y="400" click="_closeGame();" enabled="false"/>
      </mx:HBox>
      <mx:TextArea id="userMessage" wordWrap="true" editable="false" width="500" height="200"/>
      <mx:TextInput id="chatMessage" width="500"/>
    </mx:VBox>
  </mx:ViewStack>

</mx:Application>
