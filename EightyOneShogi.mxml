<?xml version="1.0" encoding="utf-8"?>
<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml" layout="absolute" creationComplete="{initApp()}" xmlns:eightyOneSquare="*">

	<mx:Script>
	  <![CDATA[
		import flash.display.*;
    import mx.events.FlexEvent;
    import flash.media.Sound;
		import mx.controls.*;
		import CsaShogiClient;
    import Board;

    private var _client:CsaShogiClient;
    [Bindable]
    private var _user_list:Array;
		private var _login_name:String;
		[Embed(source = "/sound/win.mp3")]
		private var sound_win:Class;
		private var _sound_win:Sound = new sound_win();
		[Embed(source = "/sound/lose.mp3")]
		private var sound_lose:Class;
		private var _sound_lose:Sound = new sound_lose();
		
		private function initApp():void
		{
		  trace("application initialized..");
		  _client = new CsaShogiClient();
      board.setMoveCallback(_playerMove);
      board.setTimeoutCallback(_checkTimeout);
      chatMessage.addEventListener(FlexEvent.ENTER,_handleSendChat);
      chatMessage2.addEventListener(FlexEvent.ENTER,_handleSendChat2);
      loginname.addEventListener(FlexEvent.ENTER,_handleLogin);
      password.addEventListener(FlexEvent.ENTER,_handleLogin);
      _user_list = new Array();
      userListGrid.dataProvider = _user_list;
		}

    private function _connectAndLogin():void{
		  trace("connecting.");
      _client.addEventListener(CsaShogiClient.CONNECTED,_handleConnected);
      _client.addEventListener(CsaShogiClient.LOGIN,_handleLoggedIn);
      _client.addEventListener(CsaShogiClient.GAME_STARTED,_handleGameStarted);
      _client.addEventListener(CsaShogiClient.GAME_END,_handleGameEnd);
      _client.addEventListener(CsaShogiClient.CHAT,_handleChat);
      _client.addEventListener(CsaShogiClient.MOVE,_handleMove);
      _client.addEventListener(CsaShogiClient.WHO,_handleWho);
      _client.connect();
    } 

    private function _handleLogin(e:Event):void{
      _connectAndLogin();
    }
    
    private function _handleConnected(e:Event):void{
      _client.login(loginname.text, password.text);
			_login_name = loginname.text;
    } 

    private function _handleLoggedIn(e:Event):void{
      mainViewStack.selectedIndex = 1;
      _client.who();
    }

    private function _handleChat(e:ServerMessageEvent):void{
			_writeUserMessage(e.message.substr(8));
    }

    private function _handleMove(e:ServerMessageEvent):void{
      board.makeMove(e.message);
      kifuDataGrid.dataProvider = board.kifu_list;
      kifuDataGrid.scrollToIndex(board.kifu_list.length+1);
    }

    private function _handleGameStarted(e:Event):void{
			userMessage.text = '';
      if(_client.myTurn == Kyokumen.SENTE){
        _writeUserMessage("You are Sente(Black).\n");
      } else {
        _writeUserMessage("You are Gote(White).\n");
      }
      board.startGame(_client.myTurn,_client.playerNames,60,30);
	  closeButton.enabled = false;
	  rewindAllButton.enabled = false;
	  rewindOneButton.enabled = false;
	  forwardOneButton.enabled = false;
	  forwardAllButton.enabled = false;
	  kifuDataGrid.selectable = false;
	  kifuDataGrid.dataProvider = board.kifu_list;
      mainViewStack.selectedIndex = 2;
    }

    private function _handleGameEnd(e:ServerMessageEvent):void{
      if(e.message.indexOf("TIME_UP") >= 0){
				board.timeout();
        _writeUserMessage("Time out.");
			}
      if(e.message.indexOf("LOSE") >= 0){
        _writeUserMessage("You Lose.");
        _sound_lose.play();
      } else {
        _writeUserMessage("You Win.");
        _sound_win.play();
      }
	  closeButton.enabled = true;
	  rewindAllButton.enabled = true;
	  rewindOneButton.enabled = true;
	  forwardOneButton.enabled = true;
	  forwardAllButton.enabled = true;
	  kifuDataGrid.selectable = true;
	  kifuDataGrid.selectedIndex = board.kifu_list.length;
      board.endGame();
    }

    private function _handleWho(e:ServerMessageEvent):void{
      var users:Array = e.message.split("\n");
      var user_list:Array = new Array();
      for each(var user_data:String in users){
        trace(user_data);
        if(user_data.match("##[WHO] +OK") != null){
          break;
        }
        var match:Array = user_data.match(/\#\#\[WHO\] (.*)\+(.+) x1 (\S*)/);
        if(match != null && match[2] != "OK"){
          var name:String = match[1];
          var user:Object = new Object();
          user.name = name;
          user.rating = 1000;
					user.status = match[3];
          user_list.push(user);
        }
      }
      userListGrid.dataProvider = user_list;
    }

    private function _writeUserMessage(message:String):void{
      userMessage.text += message; // + "\n";
      userMessage.callLater(_scrollDown);
      userMessage2.text += message; // + "\n";
      userMessage2.callLater(_scrollDown);
    }

    private function _scrollDown():void{
      userMessage.verticalScrollPosition = userMessage.maxVerticalScrollPosition;
    }
    
    private function _waitForGame():void{
      //_writeUserMessage("Waiting for other player to join...");
      _client.waitForGame();
			_refresh();
    }

    private function _challenge():void{
			var user:Object = userListGrid.selectedItem;
			if(user != null){
      	_client.challenge(user.name);
			}
    }

		private function _refresh():void{
			_client.who();	
		}

    private function _resign():void{
      _client.chat("(auto-chat) I resign.");
      _client.resign();
    }

    private function _who():void{
      _client.who();
    }

		private function _closeGame():void{
      mainViewStack.selectedIndex = 1;
			_refresh();
		}

		private function _userSelected():void{
			var user:Object = userListGrid.selectedItem;
			if(user != null && user.name != _login_name){
				challengeButton.enabled = true;
			} else {
				challengeButton.enabled = false;
			}
		}

    private function _playerMove(from:Point,to:Point,promote:Boolean):void{
      var mv:Movement = board.position.generateMovementFromCoordinates(from,to,promote);
      _client.move(mv.toCSA());
    }

		private function _checkTimeout():void{
			_client.checkTimeout();
		}

    private function _handleSendChat(e:FlexEvent):void{
      _client.chat(chatMessage.text);
      chatMessage.text = "";
    }
    
    private function _handleSendChat2(e:FlexEvent):void{
      _client.chat(chatMessage2.text);
      chatMessage2.text = "";
    }
    
    private function replay():void{
    	board.replayMoves(int(kifuDataGrid.selectedIndex));
    }
    
    private function replayByButton(i:int):void{
    	switch (i) {
    		case -2:
    			kifuDataGrid.selectedIndex = 0; replay();
    			break;
    		case -1:
    			if (kifuDataGrid.selectedIndex > 0) {
	    			kifuDataGrid.selectedIndex--; replay();
	    		}
	    		break;
	    	case 1:
	    		if (kifuDataGrid.selectedIndex < board.kifu_list.length) {
	    			kifuDataGrid.selectedIndex++; replay();
	    		}
	    		break;
	    	case 2:
	    		kifuDataGrid.selectedIndex = board.kifu_list.length; replay();
    	}
    	
    }

	  ]]>
	</mx:Script>
  <mx:ViewStack id="mainViewStack" creationPolicy="all" width="100%" height="100%">
    <mx:VBox id="loginBox" horizontalAlign="center" verticalAlign="middle" width="100%" height="100%">
      <mx:Label text="the 81-square Universe Shogi Dojo (powered by ShogiServer)" fontSize="12"/>
      <mx:Form>
        <mx:FormItem label="loginname">
            <mx:TextInput id="loginname" width="200"/>
	      </mx:FormItem>
	      <mx:FormItem label="password">
            <mx:TextInput id="password" width="200" displayAsPassword="true"/>
	      </mx:FormItem>
	      <mx:FormItem>
            <mx:Button id="loginButton" label="Login" y="400" click="_connectAndLogin();"/>
	      </mx:FormItem>
	    </mx:Form>
    <mx:Label id="errorMessage"/>
      <mx:Label text="Chief Programmer: Kota"/>
      <mx:Label text="ver.2010/01/08"/>
    </mx:VBox>
    
    <mx:VBox>
    	<mx:Panel title="User List Panel" width="640" height="480" paddingLeft="10" paddingTop="10">
			<mx:HBox>
	        	  <mx:Button id="startButton" label="Wait for Game" click="_waitForGame();"/>
				  <mx:Button id="challengeButton" label="Challenge" click="_challenge();" enabled="false"/>
				  <mx:Button id="refreshButton" label="Refresh" click="_refresh();"/>
			</mx:HBox>
	      <mx:DataGrid id="userListGrid" width="580" height="250" change="_userSelected();">
	       <mx:columns>
	        <mx:DataGridColumn dataField="rank" width="50"/>
	        <mx:DataGridColumn dataField="name" width="120"/>
	        <mx:DataGridColumn dataField="country" width="110"/>
	        <mx:DataGridColumn dataField="rating" width="60"/>
	        <mx:DataGridColumn dataField="status" width="120"/>
	        <mx:DataGridColumn dataField="opponent" width="120"/>
	       </mx:columns>
	      </mx:DataGrid>
	      	      <mx:TextArea id="userMessage2" wordWrap="true" editable="false" width="385" height="115"/>   
	      		  <mx:TextInput id="chatMessage2" width="290"/>    
      </mx:Panel>
    </mx:VBox>
    
    <mx:HBox>
      <mx:VBox>
      <eightyOneSquare:Board id="board" width="782" height="474"/> 
      <mx:HBox>
      	<mx:Panel id="messagePanel" title="Message Panel" width="405" height="155">
	      <mx:TextArea id="userMessage" wordWrap="true" editable="false" width="385" height="115"/>      		
      	</mx:Panel>
      	<mx:Panel id="controlPanel" title="Control Panel" width="370" height="155" verticalAlign="middle" paddingLeft="8">
      	  <mx:HBox>
       	  	<mx:Button id="resignButton" label="Resign" click="_resign();"/>
       	  	<mx:Button id="optionButton" label="Options" enabled="false"/>
          	<mx:Button id="closeButton" label="Close" click="_closeGame();" enabled="false"/>     	  	
      	  </mx:HBox>
      	  <mx:HBox>
      	  	<mx:Label text="Piece Type"/>
				<mx:RadioButtonGroup id="RadioGroup1" change="board.setPieceType(int(RadioGroup1.selectedValue));"/>
				<mx:RadioButton label="Ryouko" groupName="RadioGroup1" value="0" selected="true"/>
				<mx:RadioButton label="Kinki" groupName="RadioGroup1" value="1"/>
				<mx:RadioButton label="Hidetchi" groupName="RadioGroup1" value="2"/>
      	  </mx:HBox>
      	  <mx:HBox>
       	  	<mx:Button id="rewindAllButton" label="|&lt;" enabled="false" click="replayByButton(-2);"/>
          	<mx:Button id="rewindOneButton" label="&lt;" enabled="false" click="replayByButton(-1);"/>  	
       	  	<mx:Button id="forwardOneButton" label=">" enabled="false" click="replayByButton(1);"/>
          	<mx:Button id="forwardAllButton" label=">|" enabled="false" click="replayByButton(2);"/> 
      	  </mx:HBox>
      	  <mx:HBox>
	       	  <mx:Label text="Chat"/>
		      <mx:TextInput id="chatMessage" width="290"/>     	  	
      	  </mx:HBox>
      	</mx:Panel>
      </mx:HBox>
      </mx:VBox>
        <mx:Panel id="sidePanel" title="Side Panel" width="180" height="635">
          <mx:Label text="Watchers" fontWeight="bold"/>
	      <mx:DataGrid id="watcherListGrid" width="160" height="220">
	       <mx:columns>
	        <mx:DataGridColumn dataField="name" width="57"/>
	        <mx:DataGridColumn dataField="country" width="56"/>
	        <mx:DataGridColumn dataField="rating"/>
	       </mx:columns>
	      </mx:DataGrid>
	      <mx:Label text="Kifu" fontWeight="bold"/>
	      <mx:DataGrid id="kifuDataGrid" sortableColumns="false" width="160" height="287" change="replay();" selectable="false">
	       <mx:columns>
	        <mx:DataGridColumn headerText="No." dataField="num" width="35"/>
	        <mx:DataGridColumn dataField="move"/>
	       </mx:columns>
	      </mx:DataGrid>
	      <mx:HBox>
       	  	<mx:Button id="kifuCopyButton" label="Copy" enabled="false"/>
       	  	<mx:Button id="kifuSaveButton" label="Save" enabled="false"/>	      	
	      </mx:HBox>
      	</mx:Panel>
    </mx:HBox>
  </mx:ViewStack>

</mx:Application>
