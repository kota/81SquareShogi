<?xml version="1.0" encoding="utf-8"?>
<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml" layout="absolute" creationComplete="{initApp()}" xmlns:eightyOneSquare="*">

	<mx:Script>
	  <![CDATA[
    import flash.display.*;
    import mx.events.FlexEvent;
    import mx.events.CloseEvent;
    import flash.media.Sound;
    import mx.controls.*;
    import mx.managers.PopUpManager;
    import mx.core.IFlexDisplayObject;
    import CsaShogiClient;
    import Board;
    import ChallengeForm;
    import GameRuleEvent;

    private var _client:CsaShogiClient;
    [Bindable]
    private var _game_name:String;
    private var _user_list:Array;
		private var _login_name:String;
    private var _watch_user:Object;
    private var _monitoring:Boolean;

		[Embed(source = "/sound/win.mp3")]
		private var sound_win:Class;
		private var _sound_win:Sound = new sound_win();
		[Embed(source = "/sound/lose.mp3")]
		private var sound_lose:Class;
		private var _sound_lose:Sound = new sound_lose();
		[Embed(source = "/sound/chat.mp3")]
		private var sound_chat:Class;
		private var _sound_chat:Sound = new sound_chat();
		private var optionWindow:OptionDialog;
		private var _end_sound_play:Boolean = true;
		private var _chat_sound_play:Boolean = true;

		
		private function initApp():void
		{
		  trace("application initialized..");
		  _client = new CsaShogiClient();
      board.setMoveCallback(_playerMove);
      board.setTimeoutCallback(_checkTimeout);
      chatMessage.addEventListener(FlexEvent.ENTER,_handleSendChat);
      chatMessage2.addEventListener(FlexEvent.ENTER,_handleSendChat2);
      loginname.addEventListener(FlexEvent.ENTER,_handleLogin);
      password.addEventListener(FlexEvent.ENTER,_handleLogin);
      _user_list = new Array();
      userListGrid.dataProvider = _user_list;
		}

    private function _connectAndLogin():void{
		  trace("connecting.");
      _client.addEventListener(CsaShogiClient.CONNECTED,_handleConnected);
      _client.addEventListener(CsaShogiClient.LOGIN,_handleLoggedIn);
      _client.addEventListener(CsaShogiClient.LOGIN_FAILED,_handleLoginFailed);
      _client.addEventListener(CsaShogiClient.GAME_STARTED,_handleGameStarted);
      _client.addEventListener(CsaShogiClient.GAME_END,_handleGameEnd);
      _client.addEventListener(CsaShogiClient.CHAT,_handleChat);
      _client.addEventListener(CsaShogiClient.MOVE,_handleMove);
      _client.addEventListener(CsaShogiClient.WHO,_handleWho);
      _client.addEventListener(CsaShogiClient.MONITOR,_handleMonitor);
      _client.addEventListener(CsaShogiClient.LIST,_handleList);
      loginMessage.text = "Connection...";
      errorMessage.text = "";
      _client.connect();
    } 

    private function _handleLogin(e:Event):void{
      _connectAndLogin();
    }
    
    private function _handleConnected(e:Event):void{
      loginMessage.text = "Logging in...";
      _client.login(loginname.text, password.text);
			_login_name = loginname.text;
    } 

    private function _handleLoggedIn(e:Event):void{
      loginMessage.text = "Logged in Successfully";
      mainViewStack.selectedIndex = 1;
      _client.who();
    }

    private function _handleLoginFailed(e:ServerMessageEvent):void{
      loginMessage.text = "";
      errorMessage.text = e.message;
    }

    private function _handleChat(e:ServerMessageEvent):void{
			_writeUserMessage(e.message.substr(8));
			if (_chat_sound_play) _sound_chat.play();
    }

    private function _handleMove(e:ServerMessageEvent):void{
      board.makeMove(e.message);
      kifuDataGrid.dataProvider = board.kifu_list;
      kifuDataGrid.scrollToIndex(board.kifu_list.length+1);
    }

    private function _handleGameStarted(e:ServerMessageEvent):void{
			userMessage.text = '';
      if(_client.myTurn == Kyokumen.SENTE){
        _writeUserMessage("You are Sente(Black).\n");
      } else {
        _writeUserMessage("You are Gote(White).\n");
      }
      var match:Array = e.message.match(/^START:(.*)\+(.*)-([0-9]*)-([0-9]*)/);
      board.startGame(_client.myTurn, _client.playerNames, match[3], match[4]);
	  stopButton.enabled = false;
      closeButton.enabled = false;
      resignButton.enabled = true;
      rewindAllButton.enabled = false;
      rewindOneButton.enabled = false;
      forwardOneButton.enabled = false;
      forwardAllButton.enabled = false;
      kifuDataGrid.selectable = false;
      kifuDataGrid.dataProvider = board.kifu_list;
      mainViewStack.selectedIndex = 2;
    }

    private function _handleGameEnd(e:ServerMessageEvent):void{
      if(e.message.indexOf("TIME_UP") >= 0){
				board.timeout();
        _writeUserMessage("Time out.\n");
			}
	  if(e.message.indexOf("ILLEGAL_MOVE") >= 0){
	  	_writeUserMessage("Illegal Move.\n");
	  }
      if(e.message.indexOf("LOSE") >= 0){
        _writeUserMessage("You Lose.\n");
        if (_end_sound_play) _sound_lose.play();
      } else {
        _writeUserMessage("You Win.\n");
        if (_end_sound_play) _sound_win.play();
      }
      closeButton.enabled = true;
      resignButton.enabled = false;
      kifuCopyButton.enabled = true;
      rewindAllButton.enabled = true;
      rewindOneButton.enabled = true;
      forwardOneButton.enabled = true;
      forwardAllButton.enabled = true;
      kifuDataGrid.selectable = true;
      kifuDataGrid.selectedIndex = board.kifu_list.length;
      board.endGame();
    }

    private function _handleWho(e:ServerMessageEvent):void{
      var users:Array = e.message.split("\n");
      var user_list:Array = new Array();
      for each(var user_data:String in users){
        if(user_data.match("##[WHO] +OK") != null){
          break;
        }
        var match:Array = user_data.match(/\#\#\[WHO\] (.*)\+(.+) x1 (.*)/);
        if(match != null && match[2] != "OK"){
          var name:String = match[1];
          var user:Object = new Object();
          user.name = name;
          user.country = "81SU";
          user.rating = 1500;
          user.rank = makeRankFromRating(user.rating);
          user.status = match[3].split(" ")[0];
          if(user.status.indexOf("game_waiting") == 0){
            user.game_name = match[3].split(" ")[1];
	          user.status2 = "W";
	          match = user.game_name.match(/^(.*)-([0-9]*)-([0-9]*)/);
	          var total:int = parseInt(match[2]) / 60 ;
	          user.rule = total + "-" + match[3];
          } else if(user.status.indexOf("game") == 0){
            user.game_name = match[3].split(" ")[1];
            user.status2 = "G(" + match[3].split(" ")[2] + ")";
            match = user.game_name.match(/^(.*)-([0-9]*)-([0-9]*)/);
            total = parseInt(match[2]) / 60 ;
            user.rule = total + "-" + match[3];
            if (user.name != match[1]) user.opponent = match[1];  //Temporary Feature(Opponent is shown only for one side^^)
          }
          user_list.push(user);
        }
      }
      userListGrid.dataProvider = user_list;
    }

    private function _writeUserMessage(message:String):void{
      userMessage.text += message;
      userMessage.callLater(_scrollDown);
      userMessage2.text += message;
      userMessage2.callLater(_scrollDown);
    }

    private function _scrollDown():void{
      userMessage.verticalScrollPosition = userMessage.maxVerticalScrollPosition;
      userMessage2.verticalScrollPosition =  userMessage2.maxVerticalScrollPosition;
    }
    
    private function _waitForGame(total:int=1500,byoyomi:int=30):void{
      _client.waitForGame(total,byoyomi);
			_refresh();
			stopButton.enabled = true;
    }
    
    private function _showOptions():void{
    	optionWindow = OptionDialog(PopUpManager.createPopUp(this, OptionDialog, false));
    	PopUpManager.centerPopUp(optionWindow);
    	optionWindow.title = "Options";
    	optionWindow.RadioGroup1.selectedValue = board._piece_type;
      	optionWindow.pieceSoundCheckBox.selected = board._piece_sound_play;
    	optionWindow.gameEndSoundCheckBox.selected = _end_sound_play;
    	optionWindow.chatSoundCheckBox.selected = _chat_sound_play;
    	optionWindow.OkButton.addEventListener("click",_handleOption);
    }
    
    private function _handleOption(e:Event):void{
    	board.setPieceType(int(optionWindow.RadioGroup1.selectedValue));
    	board._piece_sound_play = optionWindow.pieceSoundCheckBox.selected;
    	_end_sound_play = optionWindow.gameEndSoundCheckBox.selected;
    	_chat_sound_play = optionWindow.chatSoundCheckBox.selected;
    }

    private function _challengeForm():void{
  		var user:Object = userListGrid.selectedItem;
			if(user != null){
        trace("gamename:"+user.game_name);
        var match:Array = user.game_name.match(/^(.*)-([0-9]*)-([0-9]*)/);
        var total:int = parseInt(match[2]) / 60 ;
        var rule:String = total + "min -" + match[3] + "sec";
        Alert.yesLabel = "Challenge";
        Alert.noLabel = "Offer your setting";
        Alert.buttonWidth = 140;
        //Only YES works now.
        Alert.show("Opponent:" + match[1] + "\nTime:" + rule + "\nHandicap: NONE","Game Setting",Alert.YES,this,_handleChallengeAlert);
        //set properties back to default.
        Alert.yesLabel = "Yes";
        Alert.noLabel = "No";
        Alert.buttonWidth = 60;
			}  	
    }

    private function _handleChallengeAlert(e:CloseEvent):void{
  		var user:Object = userListGrid.selectedItem;
      if(e.detail == Alert.YES){
        _client.challenge(user);
      } else {
        var challengeWindow:ChallengeForm = ChallengeForm(PopUpManager.createPopUp(this, ChallengeForm, true));
				PopUpManager.centerPopUp(challengeWindow);
	      challengeWindow.title = "Challenge to " + user.name;
	      challengeWindow.challengeButton.addEventListener("click",_challenge);
      }
    }

    private function _newGameForm():void{
			var newGameWindow:NewGameForm = NewGameForm(PopUpManager.createPopUp(this, NewGameForm, true));
			PopUpManager.centerPopUp(newGameWindow);
	    newGameWindow.title = "Create new game";
	    newGameWindow.addEventListener(GameRuleEvent.RULE_SELECTED,_handleRuleSelected);
    }
	
	private function _stopWaiting():void {
		_client.stopWaiting();
		_refresh();
		stopButton.enabled = false;
	}

    private function _handleRuleSelected(e:GameRuleEvent):void{
      _waitForGame(e.total,e.byoyomi);
    }

    private function _challenge(e:Event):void{
    	var user:Object = userListGrid.selectedItem;
      if(user != null){
    	  _client.challenge(user);
      }
    }

		private function _refresh():void{
			_client.who();	
		}

    private function _resign():void{
      if (board.my_turn == board.position.turn) {
      	_client.chat("(auto-chat) I resign.\n");
      	_client.resign();
      }
    }

    private function _who():void{
      _client.who();
    }

		private function _closeGame():void{
      if(_game_name && _monitoring){
        _client.monitorOff(_game_name);
        _game_name = null;
        _monitoring = false;
      }
      mainViewStack.selectedIndex = 1;
			_refresh();
		}

		private function _userSelected():void{
			var user:Object = userListGrid.selectedItem;
			if(user != null && user.name != _login_name && user.status.indexOf("game_waiting") == 0){
				challengeButton.enabled = true;
				watchButton.enabled = true;
			} else {
				challengeButton.enabled = false;
				watchButton.enabled = true;
			}
		}

    private function _playerMove(from:Point,to:Point,promote:Boolean):void{
      var mv:Movement = board.position.generateMovementFromCoordinates(from,to,promote);
      _client.move(mv.toCSA());
    }

		private function _checkTimeout():void{
			_client.checkTimeout();
		}

    private function _handleList(e:ServerMessageEvent):void{
      trace("list original message:"+e.message);
      if(_watch_user != null){
        var lines:Array = e.message.split("\n");
        for each(var line:String in lines){
          var tokens:Array = line.split("+");
          if(tokens != null && (tokens[2] == _watch_user.name || tokens[3] == _watch_user.name)){
            var match:Array = line.match(/##\[LIST\] (.*)/);
            if(match != null){
              var game_name:String = match[1];
              trace("WATCH GAME:" + game_name + "," + match[0].toString() + "," + match[1].toString());
              _game_name = game_name;
              _monitoring = true;
              _client.monitorOn(game_name);
              break;
            }
          }
        }
      }
    }

    private function _handleMonitor(e:ServerMessageEvent):void{
      if(_watch_user != null){
        board.monitor(e.message,_watch_user);
      }
    }

    private function _watch():void{
      _client.list();
      _watch_user = userListGrid.selectedItem;
      mainViewStack.selectedIndex = 2;
      closeButton.enabled = true;
      resignButton.enabled = false;
    }

    private function _handleSendChat(e:FlexEvent):void{
      _client.chat(chatMessage.text);
      chatMessage.text = "";
    }
    
    private function _handleSendChat2(e:FlexEvent):void{
      _client.chat(chatMessage2.text);
      chatMessage2.text = "";
    }
    
    private function replay():void{
    	board.replayMoves(int(kifuDataGrid.selectedIndex));
    }
    
    private function replayByButton(i:int):void{
    	switch (i) {
    		case -2:
    			kifuDataGrid.selectedIndex = 0; replay();
    			break;
    		case -1:
    			if (kifuDataGrid.selectedIndex > 0) {
	    			kifuDataGrid.selectedIndex--; replay();
	    		}
	    		break;
	    	case 1:
	    		if (kifuDataGrid.selectedIndex < board.kifu_list.length) {
	    			kifuDataGrid.selectedIndex++; replay();
	    		}
	    		break;
	    	case 2:
	    		kifuDataGrid.selectedIndex = board.kifu_list.length; replay();
    	}
    }
	
	//TO DO: Move to another class maybe?
	private function makeRankFromRating(i:int):String{
		var rank_borders:Array = new Array(50, 150, 250, 350, 450, 550, 650, 750, 850, 950, 1050, 1150, 1250, 1350, 1450, 1550, 1700, 1900, 2100, 2300, 2450,10000);
		var rank_names:Array = new Array('Novice', '15-kyu', '14-kyu', '13-kyu', '12-kyu', '11-kyu', '10-kyu', '9-kyu', '8-kyu', '7-kyu', '6-kyu', '5-kyu', '4-kyu', '3-kyu', '2-kyu', '1-kyu', '1-dan', '2-dan', '3-dan', '4-dan', '5-dan', '6-dan');
		for (var j:int = 0; j < 22; j++) {
			if (i < rank_borders[j]) return rank_names[j];
		}
		return "";
	}

	  ]]>
	</mx:Script>
  <mx:ViewStack id="mainViewStack" creationPolicy="all" width="100%" height="100%">
    <mx:VBox id="loginBox" horizontalAlign="center" verticalAlign="middle" width="100%" height="100%">
      <mx:Label text="the 81-square Universe Shogi Dojo (powered by ShogiServer)" fontSize="12"/>
      <mx:Form>
        <mx:FormItem label="loginname">
            <mx:TextInput id="loginname" width="200"/>
	      </mx:FormItem>
	      <mx:FormItem label="password">
            <mx:TextInput id="password" width="200" displayAsPassword="true"/>
	      </mx:FormItem>
	      <mx:FormItem>
            <mx:Button id="loginButton" label="Login" y="400" click="_connectAndLogin();"/>
	      </mx:FormItem>
	    </mx:Form>
      <mx:Label id="loginMessage"/>
      <mx:Label id="errorMessage" color="#ff6666" />
      <mx:LinkButton label="Sign Up" fontSize="13" click="navigateToURL(new URLRequest('http://81square-shogi.homeip.net:2195'), 'quote')" />
      <mx:Label text="Chief Programmer: Kota" fontSize="10"/>
      <mx:Label text="ver.2010/01/31"/>
    </mx:VBox>
    
    <mx:VBox>
    	<mx:Panel title="User List Panel" width="640" height="480" paddingLeft="10" paddingTop="10">
			<mx:HBox>
				<mx:Button id="refreshButton" label="Refresh" click="_refresh();"/>
				<mx:Button id="startButton" label="Wait for Game" click="_newGameForm();"/>
				<mx:Button id="stopButton" label="Stop Waiting" click="_stopWaiting()" enabled="false" />
				<mx:Button id="challengeButton" label="Challenge" click="_challengeForm();" enabled="false"/>
				<mx:Button id="watchButton" label="Watch" click="_watch();" enabled="false"/>
				<mx:Button id="optionButton1" label="Options" click="_showOptions();" enabled="false"/>
			</mx:HBox>
	      <mx:DataGrid id="userListGrid" width="580" height="250" change="_userSelected();">
	       <mx:columns>
	        <mx:DataGridColumn dataField="rank" width="65"/>
	        <mx:DataGridColumn dataField="name" width="120"/>
	        <mx:DataGridColumn dataField="country" width="110"/>
	        <mx:DataGridColumn dataField="rating" width="60"/>
			<mx:DataGridColumn dataField="status2" headerText="status" width="60"/>
			<mx:DataGridColumn dataField="rule" width="60"/>
	        <mx:DataGridColumn dataField="opponent" width="120"/>
	        <mx:DataGridColumn dataField="status" headerText="status(inside)" width="120"/>
	       </mx:columns>
	      </mx:DataGrid>
	      	      <mx:TextArea id="userMessage2" wordWrap="true" editable="false" width="385" height="115"
	      	      	 text="Note: All chat messages (even in a game room) will be seen by all users."/>   
	      		  <mx:TextInput id="chatMessage2" width="290"/>    
      </mx:Panel>
    </mx:VBox>
    
    <mx:HBox>
      <mx:VBox>
      <eightyOneSquare:Board id="board" width="782" height="474"/> 
      <mx:HBox>
      	<mx:Panel id="messagePanel" title="Message Panel" width="405" height="155">
	      <mx:TextArea id="userMessage" wordWrap="true" editable="false" width="385" height="115"/>      		
      	</mx:Panel>
      	<mx:Panel id="controlPanel" title="Control Panel" width="370" height="155" verticalAlign="middle" paddingLeft="8">
      	  <mx:HBox>
       	  	<mx:Button id="resignButton" label="Resign" click="_resign();"/>
       	  	<mx:Button id="optionButton2" label="Options" click="_showOptions();"/>
          	<mx:Button id="closeButton" label="Close" click="_closeGame();" enabled="false"/>     	  	
      	  </mx:HBox>
      	  <mx:HBox>
       	  	<mx:Button id="rewindAllButton" label="|&lt;" enabled="false" click="replayByButton(-2);"/>
          	<mx:Button id="rewindOneButton" label="&lt;" enabled="false" click="replayByButton(-1);"/>  	
       	  	<mx:Button id="forwardOneButton" label=">" enabled="false" click="replayByButton(1);"/>
          	<mx:Button id="forwardAllButton" label=">|" enabled="false" click="replayByButton(2);"/> 
      	  </mx:HBox>
      	  <mx:HBox>
	       	  <mx:Label text="Chat"/>
		      <mx:TextInput id="chatMessage" width="290"/>     	  	
      	  </mx:HBox>
      	</mx:Panel>
      </mx:HBox>
      </mx:VBox>
        <mx:Panel id="sidePanel" title="Side Panel" width="180" height="635">
          <mx:Label text="Watchers" fontWeight="bold"/>
	      <mx:DataGrid id="watcherListGrid" width="160" height="220">
	       <mx:columns>
	        <mx:DataGridColumn dataField="name" width="57"/>
	        <mx:DataGridColumn dataField="country" width="56"/>
	        <mx:DataGridColumn dataField="rating"/>
	       </mx:columns>
	      </mx:DataGrid>
	      <mx:Label text="Kifu" fontWeight="bold"/>
	      <mx:DataGrid id="kifuDataGrid" sortableColumns="false" width="160" height="287" change="replay();" selectable="false">
	       <mx:columns>
	        <mx:DataGridColumn headerText="No." dataField="num" width="35"/>
	        <mx:DataGridColumn dataField="move"/>
	       </mx:columns>
	      </mx:DataGrid>
	      <mx:HBox>
       	  	<mx:Button id="kifuCopyButton" label="Copy" click="board.copyKIFtoClipboard();" enabled="false"/>
       	  	<mx:Button id="kifuSaveButton" label="Save" enabled="false"/>	      	
	      </mx:HBox>
      	</mx:Panel>
    </mx:HBox>
  </mx:ViewStack>

</mx:Application>
