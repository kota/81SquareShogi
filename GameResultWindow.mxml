<?xml version="1.0" encoding="utf-8"?>
<mx:TitleWindow xmlns:mx="http://www.adobe.com/2006/mxml" width="335" height="105" fontSize="12" horizontalAlign="center" headerHeight="0"
                      borderThicknessBottom="3" borderThicknessLeft="3" borderThicknessRight="3" borderThicknessTop="3"
                      cornerRadius="0" horizontalScrollPolicy="off" verticalScrollPolicy="off" backgroundImage="{IMAGE_DIRECTORY}windowBg.jpg">
	    <mx:Script>
        <![CDATA[
		  import flash.events.TimerEvent;
		  import flash.utils.Timer;
          import mx.controls.*;
          import mx.managers.PopUpManager;

		  [Bindable]
		  private var _main:String;
		  [Bindable]
		  private var _you_before:String;
		  [Bindable]
		  private var _you_after:String;
		  [Bindable]
		  private var _opponent_before:String;
		  [Bindable]
		  private var _opponent_after:String;
		  [Bindable]
		  private var _you_rank:String;
		  [Bindable]
		  private var _opponent_rank:String;
		  private var _dy:int;
		  private const IMAGE_DIRECTORY:String = "http://www.81squareuniverse.com/dojo/images/";
		  private var _afterTimer:Timer = new Timer(500, 1);
		  private var _closeTimer:Timer = new Timer(4000, 1);
		  private var _fadeTimer:Timer = new Timer(50, 20);
		  
		  public function initWindow(you:Object, opponent:Object, game_name:String, v:int):void {
			  youAfter.visible = false;
			  youRank.visible = false;
			  opponentAfter.visible = false;
			  opponentRank.visible = false;
			  if (String(you.rating).match(/^\*/)) you.rating = parseInt(you.rating.substr(1));
			  if (String(opponent.rating).match(/^\*/)) opponent.rating = parseInt(opponent.rating.substr(1));
			  var match:Array = game_name.split("+")[1].match(/^([0-9a-z]+?)_(.*)-([0-9]*)-([0-9]*)/);
			  var multiply:Number = 1 + (Math.pow(parseInt(match[3]) / 300, 0.8) - 1) / (Math.pow(9, 0.8) -1);
			  if (match[1] == "r") {
				  var dif:int = InfoFetcher.ratingDif(you.rating, opponent.rating, multiply, v);
			  } else {
				  dif = 0;
			  }
			  
			  _you_before = you.name + ": " + you.rating;
			  _opponent_before = opponent.name + ": " + opponent.rating;
			  _you_rank = "";
			  _opponent_rank = "";
			  if (v > 0) {
				  _dy = -2;
				  _main = "YOU WIN!";
				  mainLabel.setStyle('color', 0xFF0000);
				  var difL:int = v > 1 ? Math.round(dif / 2.0) : dif;
				  _you_after = "-> " + (you.rating + dif) + " (+" + dif +")";
				  _opponent_after = "-> " + (opponent.rating - difL) + " (-" + difL + ")";
				  if (InfoFetcher.makeRankFromRating(you.rating) != InfoFetcher.makeRankFromRating(you.rating + dif)) {
					  _you_rank = "Rank Up!";
					  youRank.setStyle('color', 0xFF0000);
				  }
				  if (InfoFetcher.makeRankFromRating(opponent.rating) != InfoFetcher.makeRankFromRating(opponent.rating - difL)) {
					  _opponent_rank = "Rank Down";
					  opponentRank.setStyle('color', 0x0000FF);
				  }
			  } else if (v < 0) {
				  _dy = 2;
				  _main = "YOU LOST";
				  mainLabel.setStyle('color', 0x0000FF);
				  difL = v < -1 ? Math.round(dif / 2.0) : dif;
				  _you_after = "-> " + (you.rating - difL) + " (-" + difL +")";
				  _opponent_after = "-> " + (opponent.rating + dif) + " (+" + dif + ")";
				  if (InfoFetcher.makeRankFromRating(you.rating) != InfoFetcher.makeRankFromRating(you.rating - difL)) {
					  _you_rank = "Rank Down!";
					  youRank.setStyle('color', 0x0000FF);
				  }
				  if (InfoFetcher.makeRankFromRating(opponent.rating) != InfoFetcher.makeRankFromRating(opponent.rating + dif)) {
					  _opponent_rank = "Rank Up!";
					  opponentRank.setStyle('color', 0xFF0000);
				  }
			  } else {
				  _dy = 0;
				  _main = "DRAW";
				  mainLabel.setStyle('color', 0x008800);
				  _you_after = "-> " + you.rating;
				  _opponent_after = "-> " + opponent.rating;
			  }
			  _closeTimer.addEventListener(TimerEvent.TIMER_COMPLETE, _close);
			  _afterTimer.addEventListener(TimerEvent.TIMER_COMPLETE, _after);
			  _fadeTimer.addEventListener(TimerEvent.TIMER, _fade);
			  _fadeTimer.addEventListener(TimerEvent.TIMER_COMPLETE, _remove);
			  _closeTimer.start();
			  _afterTimer.start();
		  }
		  
		  private function _after(e:TimerEvent):void {
			  youAfter.visible = true;
			  youRank.visible = true;
			  opponentAfter.visible = true;
			  opponentRank.visible = true;
		  }
		  
          private function _close(e:TimerEvent):void {
			  _fadeTimer.start();
          }
		  
		  private function _fade(e:TimerEvent):void {
			  this.alpha -= 0.05;
			  this.y += _dy;
		  }
		  
		  private function _remove(e:TimerEvent):void{
			  PopUpManager.removePopUp(this);
		  }

        ]]>
    </mx:Script>
	
	<mx:Fade id="showEffect" alphaFrom="0" alphaTo="1.0" duration="1000" />
	<mx:Label id="mainLabel" text="{_main}" fontSize="19" fontWeight="bold" paddingTop="5" />
	<mx:VBox horizontalAlign="left">
		<mx:HBox>
			<mx:Label text="{_you_before}"/>
			<mx:Label id="youAfter" text="{_you_after}" fontWeight="bold" showEffect="showEffect" />
			<mx:Label id="youRank" text="{_you_rank}" fontWeight="bold" showEffect="showEffect"/>
		</mx:HBox>
		<mx:HBox>
			<mx:Label text="{_opponent_before}"/>
			<mx:Label id="opponentAfter" text="{_opponent_after}" fontWeight="bold" showEffect="showEffect"/>
			<mx:Label id="opponentRank" text="{_opponent_rank}" fontWeight="bold" showEffect="showEffect" />
		</mx:HBox>
	</mx:VBox>
</mx:TitleWindow>